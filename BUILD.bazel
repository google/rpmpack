# A build file for rpmpack.
# For running basic build/run/test you can also use the regular go tools,
# this is currently added to assist in external testing.

load("@io_bazel_rules_docker//container:container.bzl", "container_image")
load("@io_bazel_rules_go//go:def.bzl", "go_library", "go_test")
load("@bazel_tools//tools/build_defs/pkg:pkg.bzl", "pkg_tar")
load("@bazel_gazelle//:def.bzl", "gazelle")
load("//:def.bzl", "pkg_tar2rpm")

# gazelle:prefix github.com/google/rpmpack
gazelle(name = "gazelle")

go_library(
    name = "go_default_library",
    srcs = [
        "dir.go",
        "header.go",
        "rpm.go",
        "tags.go",
        "tar.go",
    ],
    importpath = "github.com/google/rpmpack",
    visibility = ["//visibility:public"],
    deps = [
        "@com_github_cavaliercoder_go_cpio//:go_default_library",
        "@com_github_pkg_errors//:go_default_library",
        "@com_github_andrew-d_lzma//:go_default_library",
        "@com_github_ulikunitz_xz//:go_default_library",
    ],
)

go_test(
    name = "go_default_test",
    srcs = [
        "dir_test.go",
        "header_test.go",
        "tar_test.go",
    ],
    data = glob(["testdata/**"]),
    embed = [":go_default_library"],
    deps = ["@com_github_google_go_cmp//cmp:go_default_library"],
)

pkg_tar(
    name = "rpmtest-tar",
    srcs = ["//:testdata/content1.txt"],
    mode = "0644",
    ownername = "root.root",
    package_dir = "var/lib/rpmpack",
)

pkg_tar2rpm(
    name = "rpmtest",
    data = ":rpmtest-tar",
    pkg_name = "rpmtest",
    release = "3.4",
    version = "1.2",
    prein = "echo \"This is preinst\" > /tmp/preinst.txt",
)

container_image(
    name = "centos_with_rpm",
    testonly = True,
    base = "@centos//image",
    directory = "/root/",
    files = [":rpmtest.rpm"],
    legacy_run_behavior = False,
)

container_image(
    name = "centos_V",
    testonly = True,
    base = ":centos_with_rpm",
    cmd = "echo ===marker===  && rpm -i /root/rpmtest.rpm && rpm -Vv rpmtest",
    legacy_run_behavior = False,
)

container_image(
    name = "centos_ls",
    testonly = True,
    base = ":centos_with_rpm",
    cmd = "echo ===marker===  && rpm -i /root/rpmtest.rpm && ls -l /var/lib/rpmpack",
    legacy_run_behavior = False,
)

container_image(
    name = "centos_preinst",
    testonly = True,
    base = ":centos_with_rpm",
    cmd = "echo ===marker===  && rpm -i /root/rpmtest.rpm && cat /tmp/preinst.txt",
    legacy_run_behavior = False,
)

sh_test(
    name = "docker_tests",
    srcs = ["docker_tests.sh"],
    data = [
        ":centos_V",
        ":centos_ls",
        ":centos_preinst",
        ":testdata/golden_V.txt",
        ":testdata/golden_ls.txt",
        ":testdata/golden_preinst.txt",
    ],
)
